plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.10'
    id 'idea'
}

group 'com.github.grpc'
version '1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.11.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.26.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

task jarServer(type: Jar) {
    archiveName = "grpc-server.jar"
    manifest {
        attributes 'Implementation-Title': 'gRPC Server Sample',
                'Implementation-Version': version,
                'Main-Class': 'com.github.lapth.greeting.server.GreetingServer'
    }
    // Copy the dependencies
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task jarClient(type: Jar) {
    archiveName = "grpc-client.jar"
    manifest {
        attributes 'Implementation-Title': 'gRPC Client Sample',
                'Implementation-Version': version,
                'Main-Class': 'com.github.lapth.greeting.client.GreetingClient'
    }
    // Copy the dependencies
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

dependencies {
    implementation 'io.grpc:grpc-netty-shaded:1.26.0'
    implementation 'io.grpc:grpc-protobuf:1.26.0'
    implementation 'io.grpc:grpc-stub:1.26.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}